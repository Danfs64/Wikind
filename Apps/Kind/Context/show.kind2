use Apps.Kind.Context as Context
use Apps.Kind.Name as Name
use Apps.Kind.Term as Term
use Apps.Kind.Printer as Printer

Context/show.type (name: Data.U60) (type: Apps.Kind.Term) (sub: Apps.Kind.Subst) (pad: Data.U60): Data.String
Context/show.type name type sub pad =
  Printer/text [ "- "
                      (Data.String.pad_right (Data.U60.to_nat pad) ' ' (Name/show name))
                      " : "
                      (Data.String.cut (Term/show (Term/fill type sub)))
                      Data.String.new_line
                    ]

Context/show.vals (name: Data.U60) (vals: Data.List Apps.Kind.Term) (sub: Apps.Kind.Subst) (pad: Data.U60): Data.String
Context/show.vals name (Data.List.nil t)           sub pad = Data.String.nil
Context/show.vals name (Data.List.cons t val vals) sub pad =
  Printer/text [ (Printer/color "2")
                      "- "
                      (Data.String.pad_right (Data.U60.to_nat pad) ' ' (Name/show name))
                      " = "
                      (Data.String.cut (Term/show (Term/fill val sub)))
                      (Printer/color "0")
                      Data.String.new_line
                      (Context/show.vals name vals sub pad)
                    ]

Context/show.go (ctx: Apps.Kind.Context) (subst: Apps.Kind.Subst) (pad: Data.U60): Data.String
Context/show.go Context/empty                       sub pad = Data.String.nil
Context/show.go (Context/entry name type vals rest) sub pad =
  Printer/text [ (Context/show.type name type sub pad)
                      (Context/show.vals name vals sub pad)
                      (Context/show.go rest sub pad)
                    ]

Context/show (ctx: Apps.Kind.Context) (subst: Apps.Kind.Subst): Data.String
Context/show ctx subst = Context/show.go ctx subst (Context/max_name_length ctx)