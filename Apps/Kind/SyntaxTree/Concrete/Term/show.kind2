use Apps.Kind.SyntaxTree.Concrete.Term as Term
use Apps.Kind.SyntaxTree.Concrete.Term.Literal as Literal
use Apps.Kind.SyntaxTree.VarName as VarName
use Apps.Kind.SyntaxTree.CtrName as CtrName
use Apps.Kind.Operator as Operator
use Data.String as String

Term/show (term: Term/) : String/Build

Term/show (Term/var _ name) = VarName/show name

Term/show (Term/ctr _ name Data.List.nil) = CtrName/show name
Term/show (Term/ctr _ name args) =
  let opener = (String/show "(")
  let name = (CtrName/show name)
  let args = (Term/show.args args)
  let closer = (String/show ")")
  String/Build.many [opener name args closer]

Term/show (Term/all _ param type body erased) =
  let erased = if erased { String/show "~" } else { String/show "" }
  let opener = (String/show "(")
  let param = match Data.Maybe param {
    some value => VarName/show value
    none => String/show "_"
  }
  let sep = (String/show " : ")
  let type = (Term/show type)
  let closer = (String/show ")")
  let arrow = (String/show " -> ")
  let body = (Term/show body)
  String/Build.many [erased opener param sep type closer arrow body]

Term/show (Term/sig_type _ param fst snd) =
  let opener = (String/show "[")
  let param = match Data.Maybe param {
    some value => VarName/show value
    none => String/show "_"
  }
  let sep = (String/show " : ")
  let fst = (Term/show fst)
  let closer = (String/show "]")
  let arrow = (String/show " -> ")
  let snd = (Term/show snd)
  String/Build.many [opener param sep fst closer arrow snd]

Term/show (Term/lambda _ param type body erased) =
  let erased = if erased { String/show "~" } else { String/show "" }
  let opener = (String/show "(")
  let param = (VarName/show param)
  let sep = (String/show " : ")
  let type = match Data.Maybe type {
    some value => Term/show value
    none => String/show "_"
  }
  let closer = (String/show ")")
  let arrow = (String/show " => ")
  let body = (Term/show body)
  String/Build.many [erased opener param sep type closer arrow body]

Term/show (Term/app _ fun args) =
  let opener = (String/show "(")
  let fun = (Term/show fun)
  let args = (Term/show.args args)
  let closer = (String/show ")")
  String/Build.many [opener fun args closer]

Term/show (Term/let _ name val next) =
  let let_ = (String/show "let ")
  let name = (VarName/show name)
  let eql = (String/show " = ")
  let val = (Term/show val)
  let sep = (String/show "; ")
  let next = (Term/show next)
  String/Build.many [let_ name eql val sep next]

Term/show (Term/ann _ val type) =
  let opener = (String/show "(")
  let val = (Term/show val)
  let closer = (String/show ")")
  let sep = (String/show " :: ")
  let type = (Term/show type)
  String/Build.many [opener val closer sep type]

Term/show (Term/lit _ lit) = Literal/show lit

Term/show (Term/binary _ op fst snd) =
  let opener = (String/show "(")
  let op = (String/show (Operator/show op))
  let space = (String/show " ")
  let fst = (Term/show fst)
  let snd = (Term/show snd)
  let closer = (String/show ")")
  String/Build.many [opener op space fst space snd closer]

Term/show (Term/hole _) = String/show "_"

// Term/show (Term/do _ type sttm) = s =>

Term/show (Term/if _ cond then_ else_) =
  let if_ = (String/show "if ")
  let cond = (Term/show cond)
  let opener = (String/show " { ")
  let closer = (String/show " }")
  let then_ = (Term/show then_)
  let else_lit = (String/show " else")
  let else_ = (Term/show else_)
  String/Build.many [if_ cond opener then_ closer else_lit opener else_ closer]

Term/show (Term/sig_pair _ fst snd) =
  let opener = (String/show "($ ")
  let fst = (Term/show fst)
  let space = (String/show " ")
  let snd = (Term/show snd)
  let closer = (String/show ")")
  String/Build.many [opener fst space snd closer]

Term/show (Term/list _ args) =
  let opener = (String/show "[")
  let args = (Term/show.args args)
  let closer = (String/show "]")
  String/Build.many [opener args closer]

Term/show (Term/subst _ name redx indx expr) =
  let spec = (String/show "specialize ")
  let name = (VarName/show name)
  let into = (String/show " into #")
  let redx = (Data.U60.show redx)
  let in = (String/show " in ")
  let expr = (Term/show expr)
  String/Build.many [spec name into redx in expr]

// Term/show (Term/match _ type scrutinee val with_vars cases motive) = s =>


Term/show (Term/open _ type var motive next) =
  let open_ = (String/show "open ")
  let type = (CtrName/show type)
  let space = (String/show " ")
  let var = (VarName/show var)
  let motive = (match Data.Maybe motive {
    some value => (s: Data.String) =>
      let sep = (String/show " : ")
      let motive = (Term/show value)
      (sep (motive s))
    none => String/show ""
  }) :: _ -> _
  let sep = (String/show "; ")
  let next = (Term/show next)
  String/Build.many [open_ type space var motive sep next]

// Term/show (Term/seqrecord _ type expr fields oper) = s =>


#inline
Term/show.args (args: Data.List Term/) : String/Build {
  String/Build.join (Data.List.map args (a => Term/show a)) " "
}
