use Data.Bits.Map as Map
use Data.Pair as Pair
use Data.Maybe as Maybe

//! Gets a value in the map at position key, copying the value without consuming the map.
Map/got <a> (map : Map/ a) (key : Data.Bits) : Pair/ (Map/ a) (Maybe/ a)
Map/got (Map/tie val lft rgt) (Data.Bits.o pred) = Map/got.lft val (Map/got lft pred) rgt
Map/got (Map/tie val lft rgt) (Data.Bits.i pred) = Map/got.rgt val lft (Map/got rgt pred)
Map/got (Map/tie val lft rgt)  Data.Bits.e       = Pair/new (Map/tie val lft rgt) val
Map/got  Map/new               key               = Pair/new Map/new Maybe/none

Map/got.lft <a> (val: Maybe/ a) (lft: Pair/ (Map/ a) (Maybe/ a)) (rgt: Map/ a) : Pair/ (Map/ a) (Maybe/ a)
Map/got.lft val (Pair/new lft res) rgt = Pair/new (Map/tie val lft rgt) res

Map/got.rgt <a> (val: Maybe/ a) (lft: Map/ a) (rgt: Pair/ (Map/ a) (Maybe/ a)) : Pair/ (Map/ a) (Maybe/ a)
Map/got.rgt val lft (Pair/new rgt res) = Pair/new (Map/tie val lft rgt) res
